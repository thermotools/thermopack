module test_trend
  use eoslibinit, only: init_thermo
  use eos, only: residualGibbs, enthalpy, entropy
  use ideal, only: idealGibbsSingle
  use thermopack_var, only: Rgas
  use funit
  implicit none
  public

contains

  @Test
  subroutine test_trend_potentials()
    ! Test ideal and residual potentials
    !
    ! MH, 2015-11-25
    integer, parameter :: ncomp = 2
    real :: t, p, x(ncomp), g, gr, gid(ncomp), h, s, gmix, gidmix
    integer :: i
    real, parameter :: tol = 1e-3

    call init_thermo('EOSCG', 'vdW', 'Classic', "CO2,H2O", 2)
    x = [0.5,0.5]
    t = 300.0
    p = 1.0e6
    call residualGibbs(t,p,x,2,gr)
    call enthalpy(t,p,x,2,h)
    call entropy(t,p,x,2,s)
    g = h - t*s
    do i=1,ncomp
      call idealGibbsSingle(t,p,i,gid(i))
    enddo
    gidmix = Rgas*T*sum(x*log(x))
    gmix = gr + sum(x*gid) + gidmix

    @assertTrue( abs((g-gmix)/g) < tol)
  end subroutine test_trend_potentials

  @Test
  subroutine test_trend_free_energy()
    use eosTV, only: free_energy_tv
    use eos, only: specificvolume
    use thermopack_constants, only: LIQPH
    ! Test Helmholtz free energy functions from TREND
    !
    ! MH, 2017-02
    integer, parameter :: ncomp = 2
    real :: t,v,x(ncomp),A,A_T,A_V,A2,A2_V,A2_T
    real :: t0,dt,v0,dv,p
    real, parameter :: tol = 1e-6, eps = 1.0e-7

    call init_thermo('EOSCG', 'vdW', 'Classic', "CO2,H2O", 2)
    x = [0.8,0.2]
    t0 = 250.0
    p = 1.0e6
    call specificvolume(t0,p,x,LIQPH,v0)
    dv = v0*eps
    dt = t0*eps

    t = t0
    v = v0
    call free_energy_tv(t,v,x,A,A_T,A_V)

    t = t0
    v = v0 + dv
    call free_energy_tv(t,v,x,A2,A2_T,A2_V)
    @assertTrue( abs(((A2-A)/dv-A_V)/A_V) < 100.0*tol)

    t = t0 + dt
    v = v0
    call free_energy_tv(t,v,x,A2,A2_T,A2_V)
    @assertTrue( abs(((A2-A)/dt-A_T)/A_T) < tol)

  end subroutine test_trend_free_energy

end module test_trend
